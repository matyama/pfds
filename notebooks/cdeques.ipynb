{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "ca4e4d90",
   "metadata": {},
   "source": [
    "# Catenable Deques\n",
    "\n",
    "### Overview\n",
    "|         instance         | persistence | amortization | empty | isEmpty |  cons  |  snoc  |     ++     | head |  tail | last |  init |\n",
    "|:------------------------:|:-----------:|:------------:|:-----:|:-------:|:------:|:------:|:----------:|:----:|:-----:|:----:|:-----:|\n",
    "|  Simple Catenable Deque  |  persistent |      yes     |  O(1) |   O(1)  | O(1)** | O(1)** | O(log(n))* | O(1) | O(1)* | O(1) | O(1)* |\n",
    "\n",
    "** amortized time*\n",
    "\n",
    "*** amortized or worst-case time (depends on the underlying deque instance)*\n",
    "\n",
    "Note that both instances assume that there is an underlying deque which supports all operations in $O(1)$ amortized or worst-case time."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "40529542",
   "metadata": {},
   "outputs": [],
   "source": [
    "class Deque q where\n",
    "\n",
    "    -- | Construct new (empty) deque\n",
    "    empty :: q a\n",
    "    \n",
    "    -- | Check whether a deque is empty\n",
    "    isEmpty :: q a -> Bool\n",
    "    \n",
    "    -- Insert, inspect and remove the front element\n",
    "    cons :: a -> q a -> q a\n",
    "    head :: q a -> a\n",
    "    tail :: q a -> q a\n",
    "    \n",
    "    -- Insert, inspect and remove the rear element\n",
    "    snoc :: q a -> a -> q a\n",
    "    last :: q a -> a\n",
    "    init :: q a -> q a\n",
    "\n",
    "class Deque d => CatenableDeque d where\n",
    "\n",
    "    -- | Concatenate two deques together\n",
    "    (++) :: d a -> d a -> d a"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "97616e68",
   "metadata": {},
   "source": [
    "## Simple Catenable Deque\n",
    "`SimpleCatDeque` is a *structural abstraction* of standard `Deque`s that implements the *implicit recursive slowdown* to provide a concatenation operator that runs in $O(log(n))$ amortized time.\n",
    "\n",
    "In the implementation and discussion below we assume that there is an existing `Deque` instance that with $O(1)$ operations (either worst-case or amortized). For example both `BankersDeque` and `RealTimeDeque` satisfy this assumption.\n",
    "\n",
    "### Representation\n",
    "`SimpleCatDeque d` structurally abstracts a *primitive* `Deque d` and as such forms a non-uniformly recursive structure. Moreover, since it follows the *implicit recursive slowdown*, this deque has two type constructors:\n",
    " 1. `Shallow` simply holds a primitive deque `d`\n",
    " 1. `Deep` has three parts: the front - a primitive deque, middle - `SimpleCatDeque` with `d` as elements and rear - also a primitive deque\n",
    " \n",
    "There is an addition invariant imposed on the `Deep` deque which states that both the front and rear primitive deques must contain at least 2 element (otherwise the deque must be downgraded to a `Shallow` one).\n",
    "\n",
    "### Operations\n",
    "Starting with the set of simple operations: `cons`, `snoc`, `head` and `last` all simply propagate to the respective operation on the underlying primitive `Deque`. That is either from the `Shallow` type or in case of a `Deep` deque according to the operation to the front or rear part.\n",
    "\n",
    "`head` and `last` take $O(1)$ steps in the worst-case and depending on the instance of the primitive deque, `cons` with `snoc` run in $O(1)$ worst-case or amortized time.\n",
    "\n",
    "Next two operations, `tail` and `init`, are symmetric in implementation and just operate in $O(1)$ amortized time on either the front or the rear part of either a deque. In case of a `Shallow`, the operation simply propagates to the primitive deque but for a `Deep` deque there are three cases to consider:\n",
    " - `tail f` (`init r`) has at lest than 2 elements => new `Deep` can safely be constructed with that part in place\n",
    " - there is not enough elements and the middle is empty => the deque downgrades into a `Shallow` one with the remaining elements transferred to the appropriate side of the rest (using either `dappendL` or `dappendR`)\n",
    " - the result is too small and the middle is non-empty => appropriate side (primitive deque) is taken from the middle and the remaining elements are transferred to it as in the previous case forming a new front or rear of new `Deep` deque\n",
    "\n",
    "Finally, the concatenation operator `++` can be proved to run in $O(log(n))$ amortized time and takes an advantage of the `Deque` operations described above. There are 4 cases depending on the operand types involved in `++` but two of them are symmetric:\n",
    " - both deques are `Shallow` => if either one is too small (has less than 2 elements) then it's appended to the appropriate side of the other and the result is `Shallow`, otherwise both operands fill the sides of a new `Deep` deque\n",
    " - one of the operands is `Shallow` and the other is `Deep` => if the shallow deque is too small then its element is moved to the appropriate side of the deep one, otherwise it replaces such side and the original front/rear is moved to the middle part\n",
    " - both deques are `Deep` => front of the former and rear of the latter deque form new front and rear and new middle is constructed by concatenating the rest of the first operand with the initial part of the second operand\n",
    "\n",
    "Last but not least, the complexity of `++` is indeed logarithmic but it can be stated more precisely with respect to the sizes $n_1$ and $n_2$ of the first and second operand respectively as $O(min(log(n_1), log(n_2)))$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "63a8bbc8",
   "metadata": {},
   "outputs": [],
   "source": [
    "data SimpleCatDeque d a = Shallow (d a) | Deep (d a) (SimpleCatDeque d (d a)) (d a)\n",
    "\n",
    "-- | Determines whether given deque 'd' contains less than 2 elements (i.e. is empty or singleton).\n",
    "-- |\n",
    "-- | Note: Deque 'd' is assumed to provide 'tail' in O(1) worst-case or amortized time.\n",
    "tooSmall :: Deque d => d a -> Bool\n",
    "tooSmall d = isEmpty d || isEmpty (tail d)\n",
    "\n",
    "-- | Rotate the head of the first deque to the front of the second one.\n",
    "-- |\n",
    "-- | Note: Deque 'd' is assumed to provide 'cons' and 'head' in O(1) worst-case or amortized time.\n",
    "dappendL :: Deque d => d a -> d a -> d a\n",
    "dappendL d1 d2 = if isEmpty d1 then d2 else cons (head d1) d2\n",
    "\n",
    "-- | Rotate the head of the second deque to the back of the first one.\n",
    "-- |\n",
    "-- | Note: Deque 'd' is assumed to provide 'snoc' and 'head' in O(1) worst-case or amortized time.\n",
    "dappendR :: Deque d => d a -> d a -> d a\n",
    "dappendR d1 d2 = if isEmpty d2 then d1 else snoc d1 (head d2)\n",
    "\n",
    "-- | 'Deque' instance for 'SimpleCatDeque d' given an arbitrary deque 'd'.\n",
    "-- |\n",
    "-- | Assumption: Deque 'd' supports all operations in O(1) time (either amortized or worst-case).\n",
    "instance Deque d => Deque (SimpleCatDeque d) where\n",
    "    \n",
    "    -- | Construct new (empty) deque.\n",
    "    empty = Shallow empty\n",
    "    \n",
    "    -- | Check whether a deque is empty.\n",
    "    isEmpty (Shallow d) = isEmpty d\n",
    "    isEmpty _ = False\n",
    "    \n",
    "    -- | Prepend an element to the front in O(1) worst-case or amortized time (depends on 'd').\n",
    "    cons x (Shallow d) = Shallow (cons x d)\n",
    "    cons x (Deep f m r) = Deep (cons x f) m r\n",
    "    \n",
    "    -- | Peek the front element of the deque in O(1) worst case time.\n",
    "    head (Shallow d) = head d\n",
    "    head (Deep f _ _) = head f\n",
    "    \n",
    "    -- | Remove the front element from a non-empty deque in O(1) amortized time.\n",
    "    -- |\n",
    "    -- | Note: By the assumtion on 'd', 'dappendL' runs in constant time (amortized or worst-case).\n",
    "    tail (Shallow d) = Shallow (tail d)\n",
    "    tail (Deep f m r)\n",
    "            | not $ tooSmall f' = Deep f' m r\n",
    "            | isEmpty m = Shallow (dappendL f' r)\n",
    "            | otherwise = Deep (dappendL f' (head m)) (tail m) r\n",
    "        where f' = tail f\n",
    "    \n",
    "    -- | Appends an element to the back in O(1) worst-case or amortized time (depends on 'd').\n",
    "    snoc (Shallow d) x = Shallow (snoc d x)\n",
    "    snoc (Deep f m r) x = Deep f m (snoc r x)\n",
    "    \n",
    "    -- | Peek the rear element of the deque in O(1) worst case time.\n",
    "    last (Shallow d) = last d\n",
    "    last (Deep _ _ r) = last r\n",
    "    \n",
    "    -- | Remove the rear element from a non-empty deque in O(1) amortized time.\n",
    "    -- |\n",
    "    -- | Note: By the assumtion on 'd', 'dappendR' runs in constant time (amortized or worst-case).\n",
    "    init (Shallow d) = Shallow (init d)\n",
    "    init (Deep f m r)\n",
    "            | not $ tooSmall r' = Deep f m r'\n",
    "            | isEmpty m = Shallow (dappendR f r')\n",
    "            | otherwise = Deep f (init m) (dappendR (last m) r')\n",
    "        where r' = init r\n",
    "\n",
    "instance Deque d => CatenableDeque (SimpleCatDeque d) where\n",
    "    \n",
    "    -- | Concatenate two deques together in O(log(n)) amortized time.\n",
    "    (Shallow d1) ++ (Shallow d2)\n",
    "        | tooSmall d1 = Shallow (dappendL d1 d2)\n",
    "        | tooSmall d2 = Shallow (dappendR d1 d2)\n",
    "        | otherwise = Deep d1 empty d2\n",
    "    (Shallow d) ++ (Deep f m r)\n",
    "        | tooSmall d = Deep (dappendL d f) m r\n",
    "        | otherwise = Deep d (cons f m) r\n",
    "    (Deep f m r) ++ (Shallow d)\n",
    "        | tooSmall d = Deep f m (dappendR r d)\n",
    "        | otherwise = Deep f (snoc m r) d\n",
    "    (Deep f1 m1 r1) ++ (Deep f2 m2 r2) = Deep f1 (snoc m1 r1 ++ cons f2 m2) r2"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "mimetype": "text/x-haskell",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "8.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
